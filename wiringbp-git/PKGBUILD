# Maintainer: StoneCold <forumi0721[at]gmail[dot]com>

pkgname=("wiringbp-bpi-git" "wiringbp-bpro-git")
_gitname_pi="bananapi"
_gitname_pro="bananapro"
pkgver=5+g18d509d+10+g941aaa1
pkgrel=1
pkgdesc="WiringBP: An implementation of most of the Arduino Wiring functions for the Banana Pi"
arch=("armv7h")
url="https://github.com/LeMaker/WiringBP/"
license=("LGPL")
makedepends=("git")
provides=("wiringpi") 
conflicts=("wiringpi")
source=("${_gitname_pi}::git+https://github.com/LeMaker/WiringBP#branch=${_gitname_pi}"
  "${_gitname_pro}::git+https://github.com/LeMaker/WiringBP#branch=${_gitname_pro}")
md5sums=("SKIP"
  "SKIP")

pkgver() {
	cd "${srcdir}/${_gitname_pi}"
  local _ver_pi=$(git rev-list --count HEAD)+g$(git rev-parse --short HEAD)
	cd "${srcdir}/${_gitname_pro}"
  local _ver_pro=$(git rev-list --count HEAD)+g$(git rev-parse --short HEAD)
  echo ${_ver_pi}+${_ver_pro}
}

prepare_common() {
	cd "${srcdir}/${1}"
	sed -i "s|/usr/local/bin/gpio|/usr/bin/gpio|" wiringPi/wiringPi.c
}

build_common() {
	cd "${srcdir}/${1}"
	make -C wiringPi all static
	make -C devLib all static INCLUDE+="-I${srcdir}/${1}/wiringPi" LDFLAGS+="-L${srcdir}/${1}/wiringPi"
	make -C gpio INCLUDE+="-I${srcdir}/${1}/wiringPi -I${srcdir}/${1}/devLib" LDFLAGS+="-L${srcdir}/${1}/wiringPi -L${srcdir}/${1}/devLib"
}

package_common(){
	install -dm0755 "${pkgdir}"/usr/include
	install -Dm0644 "${srcdir}"/"${1}"/wiringPi/*.h "${pkgdir}"/usr/include/
	install -dm0755 "${pkgdir}"/usr/lib
	install -Dm0644 "${srcdir}"/"${1}"/wiringPi/libwiringPi.{a,so*} "${pkgdir}"/usr/lib/
	
	install -dm0755 "${pkgdir}"/usr/include
	install -Dm0644 "${srcdir}"/"${1}"/devLib/*.h "${pkgdir}"/usr/include/
	install -dm0755 "${pkgdir}"/usr/lib
	install -Dm0644 "${srcdir}"/"${1}"/devLib/libwiringPiDev.{a,so*} "${pkgdir}"/usr/lib
	
	install -dm0755 "${pkgdir}"/usr/bin
	install -Dm4755 "${srcdir}"/"${1}"/gpio/gpio "${pkgdir}"/usr/bin/
	install -dm0755 "${pkgdir}"/usr/share/man/man1
	install -Dm0644 "${srcdir}"/"${1}"/gpio/gpio.1 "${pkgdir}"/usr/share/man/man1/
}

package_wiringbp-bpi-git() {
  prepare_common "${_gitname_pi}"
  build_common "${_gitname_pi}"
  package_common "${_gitname_pi}"
}

package_wiringbp-bpro-git() {
  prepare_common "${_gitname_pi}"
  build_common "${_gitname_pro}"
  package_common "${_gitname_pro}"
}

# vim:set ts=2 sw=2 et:
